apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

version project.property('version')
group 'com.ifwallet'

Properties properties = new Properties()

properties.load(project.rootProject.file('local.properties').newDataInputStream())

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        Production(MavenPublication) {
            artifact("$buildDir/outputs/aar/trustwalletcore-release.aar")
            artifact sourcesJar {
                classifier "sources"
            }
            groupId this.group
            artifactId 'wallet-core'
            version this.version

            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                configurations.implementation.allDependencies.each {
                    if (it.name != 'unspecified') {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }
    publish {
        repository {
            if (this.version.endsWith("-SNAPSHOT")) {
                repoKey = 'oss-snapshot-local'
            } else {
                repoKey = 'oss-release-local'
            }
            username = properties.getProperty("bintray.user")
            password = properties.getProperty("bintray.apikey")
        }
        defaults {
            publications 'Production'
            properties = [ 'bintray.repo': 'trust/wallet-core', 'bintray.package': 'com.trustwallet.wallet-core', 'bintray.version': this.version.toString() ]
        }
    }
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['Production']
    configurations = ['archives']
    override = true
    pkg {
        userOrg = 'hengmu'
        repo = 'wallet-core'
        name = 'com.ifwallet.wallet-core'
        description = "Cross-platform, cross-blockchain wallet library."
        publish = true
        publicDownloadNumbers = true
        licenses = ['MIT']
        vcsUrl = 'https://github.com/IFWallet/wallet-core.git'
        dryRun = false
        version {
            name = this.version
            desc = "Wallet Core ${this.version}"
            released = new Date()
            vcsTag = this.version
        }
    }
}